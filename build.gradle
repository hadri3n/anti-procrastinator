

buildscript {
    apply from: "gradle/init.gradle"
    repositories() {
        project.scriptRepo.each { mvnUrl->
            maven {url mvnUrl}
        }
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.1.0.M1'
		classpath "net.saliman:gradle-cobertura-plugin:2.2.2"
    }
}


apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'cobertura'
apply plugin: 'eclipse'

jar {
    baseName = 'aprocrastinator-backend'
    version =  '0.0.1'
}

repositories {
    project.repo.each { mvnUrl->
        maven {url mvnUrl}
    }
}

// Activation du debug sur le port 9999
applicationDefaultJvmArgs = ["-agentlib:jdwp=transport=dt_socket,address=localhost:9999,server=y,suspend=n"]

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.2.2'

    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-data-mongodb'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf'
	compile 'org.springframework.boot:spring-boot-starter-websocket'
	compile 'org.springframework:spring-messaging'

	compile 'javax.inject:javax.inject:1'
	compile 'joda-time:joda-time:2.3'
	compile 'com.fasterxml.jackson.datatype:jackson-datatype-joda:2.3.3'
	compile 'commons-io:commons-io:2.4'
	compile 'org.apache.commons:commons-lang3:3.3.2'            
	compile 'javax.validation:validation-api:1.1.0.Final'
	compile 'org.hibernate:hibernate-validator:5.1.1.Final'
	compile 'com.google.guava:guava:17.0'

    compile 'org.webjars:jquery:2.1.0-2'
	compile 'org.webjars:handlebars:1.3.0'
    compile 'org.webjars:bootstrap:3.1.1-1'
	compile 'org.webjars:q:1.0.0'
	compile 'org.webjars:stomp-websocket:2.3.1'
	compile 'org.webjars:sockjs-client:0.3.4'
	compile 'org.webjars:underscorejs:1.6.0-3'
            
    
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'org.spockframework:spock-spring:0.7-groovy-2.0'
    testCompile 'info.cukes:cucumber-junit:1.1.5'
    testCompile 'info.cukes:cucumber-groovy:1.1.5'
	testCompile 'org.assertj:assertj-core:1.6.0'
    testCompile 'com.github.groovy-wslite:groovy-wslite:0.8.0'
    testCompile 'org.mongodb:mongo-java-driver:2.12.0'
	testCompile 'org.jsoup:jsoup:1.7.2'
}

test.testLogging.exceptionFormat = 'full'

configurations {
	intTestCompile.extendsFrom testCompile
	intTestRuntime.extendsFrom testRuntime
	
    functTestCompile.extendsFrom testCompile
    functTestRuntime.extendsFrom testRuntime
}

sourceSets {
	mockMvcTest {
		groovy.srcDir 		file('src/mockMvcTest/groovy')
		resources.srcDir	file('src/mockMvcTest/resources')
		compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.intTestCompile
		runtimeClasspath = output + compileClasspath + configurations.intTestRuntime
	}
	spockTest {
		groovy.srcDir 		file('src/spockTest/groovy')
		resources.srcDir	file('src/spockTest/resources')
		compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.intTestCompile
		runtimeClasspath = output + compileClasspath + configurations.intTestRuntime
	}
    cucumberTest {
        groovy.srcDir 		file('src/cucumberTest/groovy')
        resources.srcDir	file('src/cucumberTest/resources')
        compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.functTestCompile
        runtimeClasspath = output + compileClasspath + configurations.functTestRuntime
    }
}

task mockMvcTest(type: Test) {
	testClassesDir = sourceSets.mockMvcTest.output.classesDir
	classpath = sourceSets.mockMvcTest.runtimeClasspath
}

task spockTest(type: Test) {
	testClassesDir = sourceSets.spockTest.output.classesDir
	classpath = sourceSets.spockTest.runtimeClasspath
}

task cucumberTest(type: Test) {
    testClassesDir = sourceSets.cucumberTest.output.classesDir
    classpath = sourceSets.cucumberTest.runtimeClasspath
}

cobertura {
	coverageTestTasks {
		project.tasks.withType(Test)
	}
}


check.dependsOn mockMvcTest, spockTest, cucumberTest

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}
